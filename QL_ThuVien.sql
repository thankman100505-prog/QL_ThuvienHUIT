USE master
GO
IF DB_ID('CNPM_DATABASE_THUVIEN') IS NOT NULL
BEGIN
    DROP DATABASE CNPM_DATABASE_THUVIEN
END
GO
CREATE DATABASE CNPM_DATABASE_THUVIEN
GO
USE CNPM_DATABASE_THUVIEN
GO

CREATE TABLE TACGIA
(
    MATG CHAR(7) PRIMARY KEY,
    TENTG NVARCHAR(50) NOT NULL,
    NGAYSINH DATE CHECK(NGAYSINH < GETDATE()),
    QUOCTICH NVARCHAR(50) DEFAULT N'Việt Nam'
)

CREATE TABLE THELOAI
(
    MATHELOAI CHAR(7) PRIMARY KEY,
    TENTHELOAI NVARCHAR(50) UNIQUE NOT NULL,
    MOTA NVARCHAR(255) DEFAULT N'Chưa có mô tả'
)

CREATE TABLE NHAXUATBAN
(
    MAXB CHAR(7) PRIMARY KEY,
    TENNXB NVARCHAR(100) UNIQUE NOT NULL,
    DCHI NVARCHAR(200) NOT NULL,
    SDT CHAR(10) CHECK (SDT LIKE '%[0-9]%')
)

CREATE TABLE QLSACH
(
    MASACH CHAR(7) PRIMARY KEY,
    TENSACH NVARCHAR(50) NOT NULL,
    MATG CHAR(7),
    MATHELOAI CHAR(7),
    MAXB CHAR(7),
    NAMXB INT CHECK(NAMXB <= YEAR(GETDATE())),
    SL INT CHECK(SL >= 0),
    TINHTRANG INT CHECK(TINHTRANG >= 0),
    MOTA NVARCHAR(MAX) DEFAULT N'Đang cập nhật nội dung giới thiệu...',
    FOREIGN KEY (MATG) REFERENCES TACGIA (MATG),
    FOREIGN KEY (MATHELOAI) REFERENCES THELOAI (MATHELOAI),
    FOREIGN KEY (MAXB) REFERENCES NHAXUATBAN (MAXB),
    CONSTRAINT CK_TINHTRANG CHECK (TINHTRANG <= SL)
)

CREATE TABLE BIASACH
(
    MASACH CHAR(7) PRIMARY KEY,
    URL_ANH NVARCHAR(100),
    FOREIGN KEY (MASACH) REFERENCES QLSACH(MASACH)
)

CREATE TABLE TINTUC (
    MaTin INT IDENTITY(1,1) PRIMARY KEY,
    TieuDe NVARCHAR(255) NOT NULL,
    MoTaNgan NVARCHAR(500),
    HinhAnh NVARCHAR(255),
    NgayDang DATETIME DEFAULT GETDATE(),
    LoaiTin INT, 
    HienThi BIT DEFAULT 1,
    Link VARCHAR(500)
)

CREATE TABLE ROLE_USER
(
    ROLE_ID INT IDENTITY(1,1) PRIMARY KEY,
    ROLE_NAME NVARCHAR(20) UNIQUE, 
    DESCRIPT NVARCHAR(200)
)

CREATE TABLE TAIKHOAN
(
    USERNAME CHAR(7) PRIMARY KEY, 
    PASS VARBINARY(64) DEFAULT HASHBYTES('SHA2_256', '12345'),
    ROLE_ID INT,
    FOREIGN KEY (ROLE_ID) REFERENCES ROLE_USER(ROLE_ID)
)

CREATE TABLE DOCGIA
(
    MADG CHAR(7) PRIMARY KEY,
    TENDG NVARCHAR(30) NOT NULL,
    KHOA NVARCHAR(50), 
    LOP NVARCHAR(50),
    DIACHI NVARCHAR(100),
    SODT CHAR(10) UNIQUE CHECK (SODT NOT LIKE '%[^0-9]%'),
    MAIL NVARCHAR(100) UNIQUE CHECK (MAIL LIKE '%@%') 
)

CREATE TABLE THETHUVIEN
(
    MATHE CHAR(7) PRIMARY KEY,
    MADG CHAR(7) NULL UNIQUE,
    NGAYCAP DATE,
    NGAYHETHAN DATE,
    TRANGTHAI NVARCHAR(20) DEFAULT N'Hoạt động',
    FOREIGN KEY (MADG) REFERENCES DOCGIA (MADG),
    CONSTRAINT CK_NGAYHETHAN CHECK (NGAYHETHAN > NGAYCAP)
)

CREATE TABLE QLNHANVIEN
(
    MANV CHAR(7) PRIMARY KEY,                       
    TENNV NVARCHAR(50) NOT NULL,                        
    NGSINH DATE CHECK (NGSINH < GETDATE()),               
    CHUCVU NVARCHAR(50) NOT NULL,
    SDIENTHOAI CHAR(10) UNIQUE CHECK (SDIENTHOAI LIKE '0%'), 
    MAIL NVARCHAR(100) UNIQUE CHECK (MAIL LIKE '%@%')        
)

CREATE TABLE PHIEUMUON
(
    MAPM CHAR(7) PRIMARY KEY,
    MATHE CHAR(7) NOT NULL,
    MANV CHAR(7) NULL,
    NgayMuon DATE DEFAULT GETDATE(),
    NgayDenHan DATE,
    TINHTRANG INT DEFAULT 0,
    FOREIGN KEY (MATHE) REFERENCES THETHUVIEN(MATHE),
    FOREIGN KEY (MANV) REFERENCES QLNHANVIEN(MANV),
    CONSTRAINT CK_NgayHan CHECK (NgayDenHan > NgayMuon)
)

CREATE TABLE CHITIETPM
( 
    MAPM CHAR(7),
    MASACH CHAR(7) NOT NULL,
    SLMUON INT CHECK (SLMUON > 0),
    TIENTHECHAN MONEY,
    TINHTRANG INT DEFAULT 0,
    PRIMARY KEY (MAPM,MASACH),
    FOREIGN KEY (MAPM) REFERENCES PHIEUMUON (MAPM),
    FOREIGN KEY (MASACH) REFERENCES QLSACH (MASACH)
)

CREATE TABLE PHIEUTRA
( 
    MAPT CHAR(7) PRIMARY KEY,
    MAPM CHAR(7) NOT NULL UNIQUE,
    MANV CHAR(7) NOT NULL,
    NGAYTRA DATE DEFAULT GETDATE(),
    TONG_TIENTHECHAN MONEY CHECK (TONG_TIENTHECHAN >= 0),
    TIENPHAT MONEY DEFAULT 0 CHECK (TIENPHAT >= 0),
    FOREIGN KEY (MAPM) REFERENCES PHIEUMUON (MAPM),
    FOREIGN KEY (MANV) REFERENCES QLNHANVIEN (MANV)
)

CREATE TABLE VIPHAM
(
    MAVP INT IDENTITY(1,1) PRIMARY KEY,
    MAPM CHAR(7) NOT NULL, 
    MASACH CHAR(7) NULL, 
    HINHTHUCVP NVARCHAR(50) NOT NULL CHECK (HINHTHUCVP IN (N'Trễ hẹn',N'Hỏng sách',N'Mất sách')),
    CHITIETVP NVARCHAR(500),
    SOTIENPHAT MONEY CHECK (SOTIENPHAT >= 0),
    NGAYGHINHAN DATE DEFAULT GETDATE(),
    FOREIGN KEY (MAPM) REFERENCES PHIEUMUON (MAPM)
)

CREATE TABLE PHONGHOP
(
    MAPHONG CHAR(7) NOT NULL PRIMARY KEY,
    VITRI NVARCHAR(20) NOT NULL CHECK (VITRI IN (N'Tầng 1',N'Tầng 2',N'Tầng 3',N'Tầng 4')),
    SL_NGUOITOIDA INT CHECK (SL_NGUOITOIDA > 0),
    TINHTRANG INT CHECK (TINHTRANG = 0 OR TINHTRANG = 1)
)

CREATE TABLE PHIEU_MUONPHONG
(
    MAPHIEU CHAR(7) NOT NULL PRIMARY KEY,
    MATHE CHAR(7) NOT NULL,
    MAPH CHAR(7) NOT NULL,
    NGAYMUON DATE,
    GIOMUON TIME,
    GIOTRA TIME,
    SL_NGUOITHAMGIA INT CHECK (SL_NGUOITHAMGIA > 0),
    MUCDICH NVARCHAR(500),
    TIENPHAT MONEY DEFAULT 0 CHECK (TIENPHAT >= 0),
    TINHTRANG INT DEFAULT 0,
    GHICHU_NV NVARCHAR(200),
    NGAYDUYET DATETIME NULL,
    MANV_DUYET CHAR(7) NULL,
    TRANGTHAI INT DEFAULT 0,
    FOREIGN KEY (MANV_DUYET) REFERENCES QLNHANVIEN(MANV),
    FOREIGN KEY(MAPH) REFERENCES PHONGHOP(MAPHONG),
    FOREIGN KEY (MATHE) REFERENCES THETHUVIEN(MATHE)
)

CREATE TABLE THAMSO (
    TENTHAMSO VARCHAR(50) PRIMARY KEY, 
    GIATRI INT,                                        
    MOTA NVARCHAR(200)                                 
)
GO

INSERT INTO ROLE_USER VALUES (N'Admin',N'Toàn quyền trên DB'), (N'Librarian',N'Quản lý'), (N'Reader',N'Độc giả')
INSERT INTO TACGIA VALUES
('TG00001', N'Allen B. Downey', '1970-01-01', N'Mỹ'), ('TG00002', N'Andrew S. Tanenbaum', '1944-03-16', N'Hà Lan'),
('TG00003', N'Ian Goodfellow', '1985-01-01', N'Mỹ'), ('TG00004', N'Haykin Simon', '1942-01-01', N'Canada'),
('TG00005', N'Nguyễn Thanh Tùng', '1978-05-12', N'Việt Nam'), ('TG00006', N'Tristan Nguyen', '1981-04-21', N'Việt Nam'),
('TG00007', N'Daniel Goleman', '1946-03-07', N'Mỹ'), ('TG00008', N'Dale Carnegie', '1888-11-24', N'Mỹ'),
('TG00009', N'Nguyễn Trung Hiếu', '1983-09-09', N'Việt Nam'), ('TG00010', N'Hồ Trung Thành', '1972-02-22', N'Việt Nam'),
('TG00011', N'Phạm Quốc Bảo', '1985-12-01', N'Việt Nam'), ('TG00012', N'Lê Thị Mỹ Hạnh', '1987-06-14', N'Việt Nam'),
('TG00013', N'Huỳnh Hữu Tuấn', '1989-08-18', N'Việt Nam'), ('TG00014', N'Nguyễn Hoàng Dũng', '1990-04-04', N'Việt Nam'),
('TG00015', N'Elon Musk', '1971-06-28', N'Mỹ')

INSERT INTO THELOAI VALUES
('TL00001', N'Sách chuyên ngành', N'Sách phục vụ nghiên cứu'), ('TL00002', N'Giáo trình', N'Sách phục vụ học tập'),
('TL00003', N'Tài liệu tham khảo', N'Tài liệu tra cứu'), ('TL00004', N'Sách kĩ năng', N'Sách phát triển bản thân'),
('TL00005', N'Luận văn - Khóa luận', N'Bài làm cuối khóa'), ('TL00006', N'Tạp chí khoa học', N'Tạp chí chuyên ngành')

INSERT INTO NHAXUATBAN VALUES
('NXB0001', N'NXB Giáo Dục', N'Hà Nội', '0283456789'), ('NXB0002', N'NXB Trẻ', N'TP.HCM', '0283456790'),
('NXB0003', N'NXB Khoa Học Tự Nhiên', N'Hà Nội', '0243876543'), ('NXB0004', N'NXB Đại Học Quốc Gia', N'TP.HCM', '0283451234'),
('NXB0005', N'NXB Reilly', N'Hoa Kỳ', '0012345678'), ('NXB0006', N'NXB McGraw-Hill', N'Hoa Kỳ', '0018765432'),
('NXB0007', N'NXB Pearson', N'Anh Quốc', '0044556677'), ('NXB0008', N'NXB DataSci Press', N'Singapore', '0066123456'),
('NXB0009', N'NXB Kinh Tế', N'Hà Nội', '0243688011'), ('NXB0010', N'NXB Tự Lực', N'TP.HCM', '0284562391')

INSERT INTO QLSACH VALUES
('S000001', N'Think Python', 'TG00001', 'TL00001', 'NXB0005', 2015, 12, 12, N'Lập trình Python'),
('S000002', N'Computer Networks', 'TG00002', 'TL00001', 'NXB0007', 2011, 10, 9, N'Mạng máy tính'),
('S000003', N'Deep Learning', 'TG00003', 'TL00001', 'NXB0006', 2016, 8, 8, N'Deep Learning'),
('S000004', N'Neural Networks', 'TG00004', 'TL00001', 'NXB0006', 2008, 7, 7, N'Mạng nơ-ron'),
('S000005', N'Giáo trình CSDL', 'TG00005', 'TL00002', 'NXB0001', 2020, 20, 19, N'Kiến thức CSDL')

INSERT INTO BIASACH VALUES ('S000001', 'Sach1.jpg'), ('S000002', 'Sach2.jpg'), ('S000003', 'Sach3.jpg'), ('S000004', 'Sach4.jpg'), ('S000005', 'Sach5.jpg')

INSERT INTO DOCGIA VALUES
('DG00001',N'Đỗ Thành Trung',N'Khoa CNTT',N'14DHTH12',N'TP.HCM','0912345678','trung@gmail.com'),
('DG00002',N'Lê Thị Châu',N'Khoa QTKD',N'13DHKD',N'Long An','0914425125','chau@gmail.com')

INSERT INTO THETHUVIEN VALUES ('TH00001','DG00001','2025-01-15','2027-01-15',N'Hoạt động'), ('TH00002','DG00002','2025-02-10','2027-02-10',N'Hoạt động')

INSERT INTO QLNHANVIEN VALUES
('NV00001',N'Nguyễn Văn A','1990-05-10',N'Thủ thư','0911222333','vana@gmail.com'),
('NV00002',N'Trần Thị B','1992-07-20',N'Quản lý','0911333444','thib@gmail.com')

INSERT INTO PHIEUMUON VALUES ('PM00001','TH00001','NV00001','2023-10-01','2023-10-15', 1)
INSERT INTO CHITIETPM VALUES ('PM00001','S000001',2,20000, 1)
INSERT INTO PHIEUTRA VALUES ('PT00001','PM00001','NV00001','2023-10-14',30000,0)

INSERT INTO PHONGHOP VALUES ('PH00001',N'Tầng 3',10,1), ('PH00002',N'Tầng 3',8,1)
INSERT INTO PHIEU_MUONPHONG VALUES ('MP00001','TH00001','PH00001','2025-02-11','09:50:00','10:50:00',6, N'Họp nhóm',0,0,NULL,GETDATE(),'NV00001',1)

INSERT INTO TAIKHOAN VALUES ('admin', HASHBYTES('SHA2_256', '12345'), 1)
INSERT INTO THAMSO VALUES ('SoSachToiDa', 5, N'Sách tối đa'), ('SoNgayMuon', 7, N'Số ngày mượn'), ('TienPhat', 5000, N'Tiền phạt')
GO

CREATE PROCEDURE AUTO_ID_DOCGIA @NEWID CHAR(7) OUTPUT AS
BEGIN
    DECLARE @MAX_ID INT;
    SELECT @MAX_ID = MAX(CAST(SUBSTRING(MADG, 3, 5) AS INT)) FROM DOCGIA;
    SET @NEWID = 'DG' + RIGHT('00000' + CAST(ISNULL(@MAX_ID,0) + 1 AS VARCHAR(5)), 5);
END;
GO

CREATE PROCEDURE AUTO_ID_THETHUVIEN @NEWID CHAR(7) OUTPUT AS
BEGIN
    DECLARE @MAX_ID INT;
    SELECT @MAX_ID = MAX(CAST(SUBSTRING(MATHE, 3, 5) AS INT)) FROM THETHUVIEN;
    SET @NEWID = 'TH' + RIGHT('00000' + CAST(ISNULL(@MAX_ID,0) + 1 AS VARCHAR(5)), 5);
END;
GO

CREATE PROCEDURE SP_REGISTER_DOCGIA
    @TENDG NVARCHAR(30), @KHOA NVARCHAR(50) = NULL, @LOP NVARCHAR(50) = NULL,
    @DIACHI NVARCHAR(100), @SODT CHAR(10), @MAIL NVARCHAR(100), @MATHE_OUT CHAR(7) OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
    IF EXISTS (SELECT 1 FROM DOCGIA WHERE SODT = @SODT) BEGIN ;THROW 50001, N'SĐT tồn tại!', 1; RETURN; END
    IF EXISTS (SELECT 1 FROM DOCGIA WHERE MAIL = @MAIL) BEGIN ;THROW 50002, N'Email tồn tại!', 1; RETURN; END

    BEGIN TRY
        BEGIN TRANSACTION;
        DECLARE @MADG CHAR(7); EXEC AUTO_ID_DOCGIA @MADG OUTPUT;
        INSERT INTO DOCGIA VALUES (@MADG, @TENDG, @KHOA, @LOP, @DIACHI, @SODT, @MAIL);
        DECLARE @MATHE CHAR(7); EXEC AUTO_ID_THETHUVIEN @MATHE OUTPUT;
        DECLARE @NGAYCAP DATE = GETDATE();
        INSERT INTO THETHUVIEN (MATHE, MADG, NGAYCAP, NGAYHETHAN, TRANGTHAI)
        VALUES (@MATHE, @MADG, @NGAYCAP, DATEADD(YEAR, 2, @NGAYCAP), N'Hoạt động');
        SET @MATHE_OUT = @MATHE;
        COMMIT;
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK;
        DECLARE @Msg NVARCHAR(4000) = ERROR_MESSAGE(); ;THROW 50003, @Msg, 1;
    END CATCH
END
GO

CREATE TRIGGER UPDATE_SLC_SAUKHI_MUON ON CHITIETPM AFTER INSERT AS
BEGIN
    UPDATE S SET S.TINHTRANG = S.TINHTRANG - I.SLMUON
    FROM QLSACH S JOIN inserted I ON S.MASACH = I.MASACH
END
GO

CREATE TRIGGER TRG_PHIEUTRA ON PHIEUTRA AFTER INSERT AS
BEGIN
    UPDATE S SET S.TINHTRANG = S.TINHTRANG + CT.SLMUON
    FROM QLSACH S JOIN CHITIETPM CT ON S.MASACH = CT.MASACH
    JOIN inserted I ON CT.MAPM = I.MAPM
END
GO

CREATE TRIGGER AUTO_CREATE_ACC_READER ON THETHUVIEN AFTER INSERT AS 
BEGIN
    INSERT INTO TAIKHOAN (USERNAME, ROLE_ID)
    SELECT MATHE, 3 FROM inserted I WHERE NOT EXISTS (SELECT 1 FROM TAIKHOAN WHERE USERNAME = I.MATHE)
END
GO

CREATE TRIGGER AUTO_CREATE_ACC_LIB ON QLNHANVIEN AFTER INSERT AS 
BEGIN
    INSERT INTO TAIKHOAN (USERNAME, ROLE_ID)
    SELECT MANV, 2 FROM inserted I WHERE NOT EXISTS (SELECT 1 FROM TAIKHOAN WHERE USERNAME = I.MANV)
END
GO